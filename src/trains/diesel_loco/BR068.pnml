spriteset(spriteset_BR68_Purchase, "gfx/BR68_v2.png") {
	template_purchase(0, 0)
}

spriteset(spriteset_BR68_DRS, "gfx/BR68_v2.png") {
	template_train32px_old(0, 13)
}
	
spriteset(spriteset_BR68_ScotRail, "gfx/BR68_v2.png") {
	template_train32px_old(0, 88)
}

spriteset(spriteset_BR68_Chiltern, "gfx/BR68_v2.png") {
	template_train32px_old(0, 63)
}

spriteset(spriteset_BR68_TPE, "gfx/BR68_v2.png") {
	template_train32px_old(0, 38)
}

// This switch called when displaying as this locomotive
// WARNING: If editing this, also edit PushPull.pnml in the Spritesets folder
switch(FEAT_TRAINS,SELF, sw_BR68,cargo_subtype){
    0:	spriteset_BR68_DRS;
	1: 	spriteset_BR68_ScotRail;
	2:	spriteset_BR68_Chiltern;
	3:	spriteset_BR68_TPE;
}

switch(FEAT_TRAINS,SELF,sw_BR68_cargo_subtype_text,cargo_subtype){
    0:	return string(str_DRSCompass);
	1: 	return string(str_ScotRail);
	2:	return string(str_Chiltern);
	3:	return string(str_TPE);
       return CB_RESULT_NO_TEXT;
}

// If the rear of this consist is a compatible Driving Trailer, call that vehicles's remote sprite call switch
switch(FEAT_TRAINS, PARENT, sw_BR68_PushPUll, [STORE_TEMP(num_vehs_in_consist - 1, 0x10F), var[0x61, 0, 0x0000FFFF, 0xC6]]) {	
	586: sw_PUSHPULL_LocomotiveAsMk3DVT; // Mk3 DVT
	585: sw_PUSHPULL_LocomotiveAsMk4DVT; // Mk4 DVT
	596: sw_PUSHPULL_LocomotiveAsMk5DVT; // Mk5 DVT
	sw_BR68; // No compatible locomotive found, show normal sprites
}

switch(FEAT_TRAINS,PARENT,sw_BR68_CheckPosition, position_in_consist){
    0: sw_BR68_PushPUll; // Attempt push pull
	sw_BR68; // We aren't at the front of the consist, show normal sprites
}

switch(FEAT_TRAINS,PARENT,sw_BR68_Sprites ,vehicle_is_reversed){
    0:	sw_BR68; // We're heading foward, show normal sprites
	1:	sw_BR68_CheckPosition; // Attempt push pull
}

item (FEAT_TRAINS, item_BR68, 196) {
	property {
		variant_group:					header_Diesel;
		name: 							string(STR_NAME_BR68);
		climates_available: 			ALL_CLIMATES;
		introduction_date:				date(2013,4,21);
		model_life:						VEHICLE_NEVER_EXPIRES;
		retire_early:					1;
		vehicle_life:					70;
		reliability_decay:				7;
		refittable_cargo_classes:		0;
		cargo_allow_refit:				[GOOD, FOOD];
		loading_speed:					3;
		cost_factor:					46;
		running_cost_factor:			35;
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		speed:							100 mph;
		refit_cost:						0;
		track_type:						RAIL;
		ai_special_flag:				AI_FLAG_CARGO;
		power:							3800 hp;
		running_cost_base:				RUNNING_COST_DIESEL;
		dual_headed:					0;
		default_cargo_type:				GOOD;
		cargo_capacity:					1;
		weight:							85 ton;
		engine_class:					ENGINE_CLASS_DIESEL;
		tractive_effort_coefficient:	0.31;
		air_drag_coefficient:			0.1;
		length:							8;
		effect_spawn_model_and_powered:	EFFECT_SPAWN_MODEL_DIESEL;
		extra_weight_per_wagon:			0;
		bitmask_vehicle_info:			0;
	}
	graphics {
	additional_text:					return(string(str_purchase_loco_with_liveries,string(str_purchase_type_diesel),string(str_BR68_usage),string(str_BR68_eos),string(str_BR68_liveries)));
	can_attach_wagon:					CB_RESULT_ATTACH_ALLOW;
	cargo_capacity:						return 0;
	cargo_subtype_text:					sw_BR68_cargo_subtype_text;
	default: 							sw_BR68_Sprites;
	purchase:							spriteset_BR68_Purchase;
	colour_mapping: 					return PALETTE_CC_FIRST;
	create_effect:						diesel_create_visual_effect;
	sound_effect:						sw_66_sound;
	cost_factor:	return(GetAdjustedCost(46));
	running_cost_factor: return(GetAdjustedCost(35));
}
}
