// Choose sprites depending on car position
switch(FEAT_TRAINS, SELF, switch_br_231_spriteset, position_in_articulated_veh % 5) {
	0: spriteset_BR231_front;
    1: spriteset_BR231_fronttrailer;
	2: spriteset_BR231_powerpack;
    3: spriteset_BR231_reartrailer;
	4: spriteset_BR231_rear;
}

// Define articulations
switch(FEAT_TRAINS, SELF, switch_br_231_articulated, extra_callback_info1) {
	1..4: return item_br_231;
	return CB_RESULT_NO_MORE_ARTICULATED_PARTS; // Stop adding more cars
}

switch(FEAT_TRAINS, SELF, switch_br_231_particles_electric, position_in_articulated_veh % 5) {
	3: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
	visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}

switch(FEAT_TRAINS, SELF, switch_br_231_particles_diesel, position_in_articulated_veh % 5) {
	2: visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 2, DISABLE_WAGON_POWER);
	visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}

switch(FEAT_TRAINS, SELF, switch_br_231_particles, tile_powers_railtype("ELRL")) {
	1: switch_br_231_particles_electric;
	switch_br_231_particles_diesel;
}

// Start/stop switch, used to limit maximum length
switch(FEAT_TRAINS, SELF, switch_br_231_start_stop, num_vehs_in_consist) {	
	return CB_RESULT_NO_TEXT;   // Allow any length
}

// Only allow the same train type to be added to itself
switch(FEAT_TRAINS, SELF, switch_br_231_can_attach_wagon, vehicle_type_id) {
	item_br_231: return CB_RESULT_ATTACH_ALLOW;	
	return string(STR_CANNOT_ATTACH);
}

switch(FEAT_TRAINS, SELF, switch_br_231_length, position_in_articulated_veh % 5) {
	1: return 6;
	2: return 5;
	3: return 6;
	return 7;
}

// Class 755/4 (4.5car)
item(FEAT_TRAINS, item_br_231, 574) {
	property {
		variant_group:			header_Flirt;
		name: string(STR_BR_231_4CAR);
		climates_available: ALL_CLIMATES; // available in all climates
		introduction_date: date(2019, 07, 29);
		model_life: VEHICLE_NEVER_EXPIRES; // keep available forever		
		vehicle_life: 30; // years after vehicle is deemed "old" and should be replaced
		reliability_decay: 0; // dont reduce reliabilty, (will grow from 75% upwards over the years)
		refittable_cargo_classes: bitmask(CC_PASSENGERS);
		non_refittable_cargo_classes: bitmask();
		cost_factor: 76;
		loading_speed: 15;
		running_cost_factor: 25;
		cargo_allow_refit: [];
		cargo_disallow_refit: [];
		
		speed: 90 mph;
		power: 2000 hp;
		cargo_capacity: (229/4);
		weight: 114.3 ton; 
		
		sprite_id: SPRITE_ID_NEW_TRAIN; // required
		refit_cost: 0;
		track_type: RAIL;
		ai_special_flag: AI_FLAG_PASSENGER; // tell ai that this is a passenger train
		running_cost_base: RUNNING_COST_ELECTRIC;
		dual_headed: 0; // set to 0 otherwise limited to 2 cars only
		engine_class: ENGINE_CLASS_DIESEL; // even if its 3rd rail, ELECTRIC would give overhead wire effects
		extra_power_per_wagon: 0 kW;
		tractive_effort_coefficient: 0.3;
		air_drag_coefficient: 0.06;
		length: 7; // full tile length vehicle
		visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER); // visual fx
		extra_weight_per_wagon: 0 ton;
		bitmask_vehicle_info: 0;
	}
	
	graphics {
		default: switch_br_231_spriteset;
		purchase: spriteset_br755_purchase;

		articulated_part: switch_br_231_articulated;
		start_stop: switch_br_231_start_stop;	
		can_attach_wagon: switch_br_231_can_attach_wagon;
		visual_effect_and_powered: switch_br_231_particles;		
		length: switch_br_231_length;
		additional_text: string(STR_DSC_BR_231);
		cargo_capacity:						return(229/4 * param_pax);	
		cost_factor:	return(GetAdjustedCost(76));
		running_cost_factor: return(GetAdjustedCost(25));	
	}
}