// Choose sprites depending on car position
switch(FEAT_TRAINS, SELF, switch_br_196_0_spriteset, cargo_subtype) {
	0: sw_BR196_WMR;
	1: sw_BR196_Chiltern;
}

switch(FEAT_TRAINS, SELF, switch_br_196_0_liveries, cargo_subtype) {
	0: return string(str_WMR);
	1: return string(str_Chiltern);
}


// Define articulations
switch(FEAT_TRAINS, SELF, switch_br_196_0_articulated, extra_callback_info1) {
	// extra_callback_info1 returns the car number, so 1 is the second car, .. so on
	// The callback runs this switch until it returns NO_MORE_ARTICULATED_PARTS, which ends the growth
	1: return item_br_196_0;
	return CB_RESULT_NO_MORE_ARTICULATED_PARTS; // Stop adding more cars
}

// Start/stop switch, used to limit maximum length
switch(FEAT_TRAINS, SELF, switch_br_196_0_start_stop, num_vehs_in_consist) {	
	return CB_RESULT_NO_TEXT;   // Allow any length
}

// Only allow the same train type to be added to itself
switch(FEAT_TRAINS, SELF, switch_br_196_0_can_attach_wagon, vehicle_type_id) {
	item_br_196_0: return CB_RESULT_ATTACH_ALLOW;
	item_br_196_1: return CB_RESULT_ATTACH_ALLOW;
	return string(STR_CANNOT_ATTACH);
}

// Class 196/0 (2car)
item(FEAT_TRAINS, item_br_196_0, 432) {
	property {
		variant_group:					header_Civity;
		name: string(STR_BR_196_2CAR);
		climates_available: ALL_CLIMATES; // available in all climates
		introduction_date: date(2021, 07, 01);
		model_life: VEHICLE_NEVER_EXPIRES; // keep available forever		
		vehicle_life: 35; // years after vehicle is deemed "old" and should be replaced
		reliability_decay: 0; // dont reduce reliabilty, (will grow from 75% upwards over the years)
		refittable_cargo_classes: bitmask(CC_PASSENGERS);
		non_refittable_cargo_classes: bitmask();
		cost_factor: 42;
		loading_speed: 25;
		running_cost_factor: 25;
		cargo_allow_refit: [];
		cargo_disallow_refit: [];
		
		speed: 100 mph;
		power: 1036 hp;
		cargo_capacity: (124/2);
		weight: 87.4 ton; 
		
		sprite_id: SPRITE_ID_NEW_TRAIN; // required
		refit_cost: 0;
		track_type: RAIL;
		ai_special_flag: AI_FLAG_PASSENGER; // tell ai that this is a passenger train
		running_cost_base: RUNNING_COST_DIESEL;
		dual_headed: 0; // set to 0 otherwise limited to 2 cars only
		engine_class: ENGINE_CLASS_DIESEL; // even if ts 3rd rail, ELECTRIC would give overhead wire effects
		extra_power_per_wagon: 0 kW;
		tractive_effort_coefficient: 0.3;
		air_drag_coefficient: 0.06;
		length: 8; // full tile length vehicle
		visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 2, DISABLE_WAGON_POWER); // visual fx
		extra_weight_per_wagon: 0 ton;
		bitmask_vehicle_info: 0;
	}
	
	graphics {
		default: switch_br_196_0_spriteset;
		purchase: spriteset_BR196_Purchase;
		cargo_subtype_text: switch_br_196_0_liveries;
		
		articulated_part: switch_br_196_0_articulated;
		start_stop: switch_br_196_0_start_stop;	
		can_attach_wagon: switch_br_196_0_can_attach_wagon;
		visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 2, DISABLE_WAGON_POWER);
		cargo_capacity: return(124 / 2 * param_pax);		
		additional_text: string(STR_DSC_BR_196);	
		cost_factor:	return(GetAdjustedCost(42));
		running_cost_factor: return(GetAdjustedCost(25));
	}
}