spriteset(spriteset_BR87_Purchase, "gfx/Electric/BR8X.png") {
	template_purchase(264, 0)
}

spriteset(spriteset_BR87_InterCitySwallow, "gfx/Electric/BR8X.png") {
	template_train32px_old(0, 213)
}

spriteset(spriteset_BR87_Virgin, "gfx/Electric/BR8X.png") {
	template_train32px_old(0, 238)
}

spriteset(spriteset_BR87_BRBlue, "gfx/Electric/BR8X.png") {
	template_train32px_old(0, 263)
}

// This switch called when displaying as this locomotive
// WARNING: If editing this, also edit PushPull.pnml in the Spritesets folder
switch(FEAT_TRAINS,SELF, sw_BR87, cargo_subtype){
    0:	spriteset_BR87_BRBlue;
	1:	spriteset_BR87_InterCitySwallow;
	2:	spriteset_BR87_Virgin;
        return CB_RESULT_NO_TEXT;
}

switch(FEAT_TRAINS,SELF,sw_BR87_cargo_subtype_text,cargo_subtype){
    0:	return string(str_BRBlue);	
    1:	return string(str_InterCitySwallow);
	2:	return string(str_Virgin);
       return CB_RESULT_NO_TEXT;
}

// If the rear of this consist is a compatible Driving Trailer, call that vehicles's remote sprite call switch
switch(FEAT_TRAINS, PARENT, sw_BR87_PushPUll, [STORE_TEMP(num_vehs_in_consist - 1, 0x10F), var[0x61, 0, 0x0000FFFF, 0xC6]]) {	
	586: sw_PUSHPULL_LocomotiveAsMk3DVT; // Mk3 DVT
	sw_BR87; // No compatible locomotive found, show normal sprites
}

switch(FEAT_TRAINS,PARENT,sw_BR87_CheckPosition, position_in_consist){
    0: sw_BR87_PushPUll; // Attempt push pull
	sw_BR87; // We aren't at the front of the consist, show normal sprites
}

switch(FEAT_TRAINS,PARENT,sw_BR87_Sprites ,vehicle_is_reversed){
    0:	sw_BR87; // We're heading foward, show normal sprites
	1:	sw_BR87_CheckPosition; // Attempt push pull
}

item (FEAT_TRAINS, item_BR87, 567) {
	property {
		name: 							string(STR_NAME_BR87);
		climates_available: 			ALL_CLIMATES;
		introduction_date:				date(1973,1,1);
		model_life:						30;
		retire_early:					1;
		vehicle_life:					70;
		reliability_decay:				7;
		refittable_cargo_classes:		0;
		cargo_allow_refit:				[GOOD, FOOD];
		loading_speed:					3;
		cost_factor:					40;
		running_cost_factor:			40;
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		speed:							110 mph;
		misc_flags:						TRAIN_FLAG_FLIP;
		refit_cost:						0;
		track_type:						ELRL;	
		ai_special_flag:				AI_FLAG_CARGO;
		power:							5000 hp;
		running_cost_base:				RUNNING_COST_ELECTRIC;
		dual_headed:					0;
		default_cargo_type:				GOOD;
		cargo_capacity:					1;
		weight:							80 ton;
		engine_class:					ENGINE_CLASS_ELECTRIC;
		tractive_effort_coefficient:	0.3;
		air_drag_coefficient:			0.05;
		length:							8;
		effect_spawn_model_and_powered:	EFFECT_SPAWN_MODEL_ELECTRIC;
		extra_weight_per_wagon:			0;
		bitmask_vehicle_info:			0;
	}
	graphics {
	additional_text:					return(string(str_purchase_loco_with_liveries,string(str_purchase_type_electric),string(str_route_7),string(str_BR87_usage),string(str_BR87_eos),string(str_BR87_liveries)));
	can_attach_wagon:					CB_RESULT_ATTACH_ALLOW;
	cargo_capacity:						return 0;
	cargo_subtype_text:					sw_BR87_cargo_subtype_text;
	default: 							sw_BR87_Sprites;
	purchase:							spriteset_BR87_Purchase;
	colour_mapping: 					return PALETTE_CC_FIRST;
	cost_factor:	return(GetAdjustedCost(40));
	running_cost_factor: return(GetAdjustedCost(40));
}
}
