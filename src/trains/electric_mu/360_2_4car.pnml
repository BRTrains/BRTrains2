

switch(FEAT_TRAINS, SELF, sw_BR360_2_4car_HeathrowConnect, position_in_articulated_veh % 4)
{
	0: spriteset_BR360_front_HeathrowConnect;
	1: spriteset_BR360_middle_HeathrowConnect;
	2: spriteset_BR360_middlepanto_HeathrowConnect;
	3: spriteset_BR360_rear_HeathrowConnect;
}

switch(FEAT_TRAINS,SELF, sw_BR360_2_4car,cargo_subtype){
    0:	sw_BR360_2_4car_HeathrowConnect;	
}

switch (FEAT_TRAINS, SELF, switch_articulated_BR360_2_4car, extra_callback_info1) {
	1..3: return item_BR360_2_4car;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS; //stop adding vehicle parts
}

switch(FEAT_TRAINS,SELF,sw_BR360_2_4car_cargo_subtype_text,cargo_subtype){
    0:	return string(str_HeathrowConnect);	
       return CB_RESULT_NO_TEXT;
}

item (FEAT_TRAINS, item_BR360_2_4car, 738) {
	property {		
		name: 							string(STR_NAME_BR360_2_4car);
		climates_available: 			ALL_CLIMATES;
		introduction_date:				date(2004,4,1);
		model_life:						15;
		retire_early:					1;
		vehicle_life:					50;
		reliability_decay:				7;
		refittable_cargo_classes:		CC_PASSENGERS;
		cargo_allow_refit:				[PASS, TOUR];
		loading_speed:					25;
		cost_factor:					42;
		running_cost_factor:			38;
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		speed:							100 mph;
		refit_cost:						0;
		track_type:						ELRL;
		ai_special_flag:				AI_FLAG_CARGO;
		power:							2080 hp;
		running_cost_base:				RUNNING_COST_ELECTRIC;
		dual_headed:					0;
		default_cargo_type:				PASS;
		cargo_capacity:					1;
		weight:							168 ton;
		engine_class:					ENGINE_CLASS_ELECTRIC;
		tractive_effort_coefficient:	0.3;
		air_drag_coefficient:			0.2;
		length:							8;
		effect_spawn_model_and_powered:	EFFECT_SPAWN_MODEL_ELECTRIC;
		extra_weight_per_wagon:			0;
		bitmask_vehicle_info:			0;
		cargo_age_period:				185;
	}
	graphics {
	additional_text:					return(string(str_purchase_loco_with_liveries,string(str_purchase_type_emu),string(str_BR360_usage),string(str_BR360_eos),string(str_BR360_2_4car_liveries)));
	can_attach_wagon:					CB_RESULT_ATTACH_ALLOW;
	cargo_subtype_text:					sw_BR360_2_4car_cargo_subtype_text;
	articulated_part:        			switch_articulated_BR360_2_4car;
	default: 							sw_BR360_2_4car;
	purchase:							spriteset_BR360_2_4car_Purchase;
	colour_mapping: 					return PALETTE_CC_FIRST;
	create_effect:						electric_create_visual_effect;
	sound_effect:						sw_sound_electric_350;
	cargo_capacity: return(280 * param_pax / 4);
		 cost_factor:	return(GetAdjustedCost(40));
	running_cost_factor: return(GetAdjustedCost(36));
}
}