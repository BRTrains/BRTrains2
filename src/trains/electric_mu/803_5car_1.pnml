// Choose sprites depending on car position
switch(FEAT_TRAINS, SELF, switch_BR803_5car_spriteset, position_in_articulated_veh % 5) {
	0: spriteset_BR80X_front_Lumo;
	4: spriteset_BR80X_rear_Lumo;
	spriteset_BR80X_middle_Lumo;
}

// Define articulations
switch(FEAT_TRAINS, SELF, switch_BR803_5car_articulated, extra_callback_info1) {
	// extra_callback_info1 returns the car number, so 1 is the second car, 2 the third, .. so on
	// The callback runs this switch until it returns NO_MORE_ARTICULATED_PARTS, which ends the growth
	1..4: return item_BR803_5car;
	return CB_RESULT_NO_MORE_ARTICULATED_PARTS; // Stop adding more cars
}

// Only give particle effects to front/rear cars
switch(FEAT_TRAINS, SELF, switch_BR803_5car_particles, position_in_articulated_veh % 5) {
	// 0 is first car, 1 is second car, 2 is third car, so on...
	0: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
	4: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
	visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}

// Start/stop switch, used to limit maximum length
switch(FEAT_TRAINS, SELF, switch_BR803_5car_start_stop, num_vehs_in_consist) {	
	return CB_RESULT_NO_TEXT;   // Allow any length
}

// Only allow the same train type to be added to itself
switch(FEAT_TRAINS, SELF, switch_BR803_5car_can_attach_wagon, vehicle_type_id) {
	item_BR803_5car: return CB_RESULT_ATTACH_ALLOW;	
	return string(STR_CANNOT_ATTACH);
}

item(FEAT_TRAINS, item_BR803_5car, 547) {
	property {
		variant_group:				header_IEP;
		name: string(STR_BR803_5CAR);
		climates_available: ALL_CLIMATES; // available in all climates
		introduction_date: date(2019, 09, 16);
		model_life: VEHICLE_NEVER_EXPIRES; // keep available forever		
		vehicle_life: 27.5; // years after vehicle is deemed "old" and should be replaced
		reliability_decay: 0; // dont reduce reliabilty, (will grow from 75% upwards over the years)
		refittable_cargo_classes: bitmask(CC_PASSENGERS);
		non_refittable_cargo_classes: bitmask();
		cost_factor: 84;
		loading_speed: 12;
		running_cost_factor: 60;
		cargo_allow_refit: [];
		cargo_disallow_refit: [];
		
		speed: 125 mph;
		power: 3636 hp;
		cargo_capacity: (302/5);
		weight: 205 ton;
		
		sprite_id: SPRITE_ID_NEW_TRAIN; // required
		refit_cost: 0;
		track_type: ELRL;
		ai_special_flag: AI_FLAG_PASSENGER; // tell ai that this is a passenger train
		running_cost_base: RUNNING_COST_ELECTRIC;
		dual_headed: 0; // set to 0 otherwise limited to 2 cars only
		engine_class: ENGINE_CLASS_ELECTRIC; // even if its 3rd rail, ELECTRIC would give overhead wire effects
		extra_power_per_wagon: 0 kW;
		tractive_effort_coefficient: 0.3;
		air_drag_coefficient: 0.06;
		length: 8; // full tile length vehicle
		visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER); // visual fx
		extra_weight_per_wagon: 0 ton;
		bitmask_vehicle_info: 0;
	}
	
	graphics {
		default: switch_BR803_5car_spriteset;
		purchase: spriteset_BR803_purchase;
		
		speed: switch_BR800_speed;

		articulated_part: switch_BR803_5car_articulated;
		start_stop: switch_BR803_5car_start_stop;	
		can_attach_wagon: switch_BR803_5car_can_attach_wagon;
		visual_effect_and_powered: switch_BR803_5car_particles;
		additional_text: string(STR_DSC_BR803);	
		cargo_capacity:						return(302/5 * param_pax);	
		cost_factor:	return(GetAdjustedCost(84));
	running_cost_factor: return(GetAdjustedCost(60));
	
	}
}