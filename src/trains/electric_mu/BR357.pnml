switch (FEAT_TRAINS, SELF, switch_articulated_BR357, extra_callback_info1) {
	1..3: return item_BR357;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS; //stop adding vehicle parts
}

switch(FEAT_TRAINS,SELF,sw_BR357,cargo_subtype){
    0:	sw_BR357_C2CWhite;
	1:	sw_BR357_C2CBlue;
	2:	sw_BR357_C2CGoGold;
	3:	sw_BR357_C2CPink;
	4:	sw_BR357_C2CGreen;
	5:	sw_BR357_LTS;
	 return CB_RESULT_NO_TEXT;
}

switch(FEAT_TRAINS,SELF,sw_BR357_cargo_subtype_text,cargo_subtype){
    0:	return string(STR_LIVERY, string(str_comp_C2C), string(str_col_White));
	1:	return string(STR_LIVERY, string(str_comp_C2C), string(str_col_Blue));
	2:	return string(STR_LIVERY, string(str_comp_C2C), string(str_special_GoGold));
	3:	return string(STR_LIVERY, string(str_comp_C2C), string(str_col_Pink));
	4:	return string(STR_LIVERY, string(str_comp_C2C), string(str_col_Green));
	5:	return string(str_LTS);
	 return CB_RESULT_NO_TEXT;
}

switch(FEAT_TRAINS, SELF, sw_BR357_cargo_capacity, position_in_articulated_veh_from_end)
{
	0: return(68 * param_pax);
	4: return(68 * param_pax);
	return(74 * param_pax);
}

item (FEAT_TRAINS, item_BR357, 531) {
	property {
		variant_group:					header_Electrostar;
		name: 							string(STR_NAME_BR357);
		climates_available: 			ALL_CLIMATES;
		introduction_date:				date(1999,1,1);
		model_life:						15;
		retire_early:					1;
		vehicle_life:					50;
		reliability_decay:				7;
		refittable_cargo_classes:		CC_PASSENGERS;
		cargo_allow_refit:				[PASS, TOUR];
		loading_speed:					35;
		cost_factor:					46;
		running_cost_factor:			58;
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		speed:							100 mph;
		refit_cost:						0;
		track_type:						ELRL;
		ai_special_flag:				AI_FLAG_CARGO;
		power:							2682 hp;
		running_cost_base:				RUNNING_COST_ELECTRIC;
		dual_headed:					0;
		default_cargo_type:				PASS;
		cargo_capacity:					68;
		weight:							191 ton;
		engine_class:					ENGINE_CLASS_ELECTRIC;
		tractive_effort_coefficient:	0.3;
		air_drag_coefficient:			0.2;
		length:							8;
		effect_spawn_model_and_powered:	EFFECT_SPAWN_MODEL_ELECTRIC;
		extra_weight_per_wagon:			0;
		bitmask_vehicle_info:			0;
		cargo_age_period:				139;
	}
	graphics {
		additional_text:			return(string(str_purchase_loco_with_liveries,string(str_purchase_type_emu),string(str_BR357_usage),string(str_BR357_eos),string(str_BR357_liveries)));
		can_attach_wagon:			CB_RESULT_ATTACH_ALLOW;
		cargo_subtype_text:			sw_BR357_cargo_subtype_text;
		articulated_part:        	switch_articulated_BR357;
		default: 					sw_BR357;
		purchase:					spriteset_BR357_Purchase;
		colour_mapping: 			return PALETTE_CC_FIRST;
		create_effect:				electric_create_visual_effect;
		sound_effect:				sw_sound_electric_88;
		cargo_capacity: 			sw_BR357_cargo_capacity;
		cost_factor:				return(GetAdjustedCost(46));
		running_cost_factor: 		return(GetAdjustedCost(58));
	}
}