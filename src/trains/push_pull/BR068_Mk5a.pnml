spriteset (spriteset_BR68_5a_purchase, "gfx/BR68_v2.png") {
	template_purchase(88, 0)
}

spriteset (spriteset_BRMk5a_dvt_TPE, "gfx/Rolling Stock/Mk5a.png") {
	template_train32px(0, 13)
}
spriteset (spriteset_BRMk5a_dvt_reversed_TPE, "gfx/Rolling Stock/Mk5a.png") {
	template_train32px(0, 39)
}
spriteset (spriteset_BRMk5a_middle_TPE, "gfx/Rolling Stock/Mk5a.png") {
	template_train32px(0, 65)
}
spriteset (spriteset_BRMk5a_middle_reversed_TPE, "gfx/Rolling Stock/Mk5a.png") {
	template_train32px(0, 91)
}
switch(FEAT_TRAINS, PARENT, sw_BRMk5a_middle_TPE, vehicle_is_reversed) {
	0: spriteset_BRMk5a_middle_TPE;
	1: spriteset_BRMk5a_middle_reversed_TPE;
}

spriteset (spriteset_BRMk5a_dvt_reversed_ChilternBlue, "gfx/Rolling Stock/Mk5a.png") {
	template_train32px(0, 143)
}
spriteset (spriteset_BRMk5a_dvt_ChilternBlue, "gfx/Rolling Stock/Mk5a.png") {
	template_train32px(0, 117)
}
spriteset (spriteset_BRMk5a_middle_ChilternBlue, "gfx/Rolling Stock/Mk5a.png") {
	template_train32px(0, 169)
}
spriteset (spriteset_BRMk5a_middle_reversed_ChilternBlue, "gfx/Rolling Stock/Mk5a.png") {
	template_train32px_reversed(0, 169)
}
switch(FEAT_TRAINS, PARENT, sw_BRMk5a_middle_ChilternBlue, vehicle_is_reversed) {
	0: spriteset_BRMk5a_middle_ChilternBlue;
	1: spriteset_BRMk5a_middle_reversed_ChilternBlue;
}

switch(FEAT_TRAINS, PARENT, sw_BR68_5a_locomotive, cargo_subtype) {
	0: spriteset_BR68_TPE;
	1: sw_BR68_ChilternBlue;
}

switch(FEAT_TRAINS, SELF, sw_BR68_5a_middle, cargo_subtype) {
	0: sw_BRMk5a_middle_TPE;
	1: sw_BRMk5a_middle_ChilternBlue;
}


switch(FEAT_TRAINS, SELF, sw_BR68_5a_DVT, cargo_subtype) {
	0: spriteset_BRMk5a_dvt_reversed_TPE;
	1: spriteset_BRMk5a_dvt_reversed_ChilternBlue;
}

switch(FEAT_TRAINS, SELF, sw_BR68_5a_DVT_reversed, cargo_subtype) {
	0: spriteset_BRMk5a_dvt_TPE;
	1: spriteset_BRMk5a_dvt_ChilternBlue;
}


switch(FEAT_TRAINS, PARENT, sw_BR68_5a_DVT_pushpull, vehicle_is_reversed) {
	0: sw_BR68_5a_DVT;
	1: sw_BR68_5a_locomotive;
}

switch(FEAT_TRAINS, PARENT, sw_BR68_5a_locomotive_pushpull, vehicle_is_reversed) {
	0: sw_BR68_5a_locomotive;
	1: sw_BR68_5a_DVT_reversed;
}

// Choose sprites depending on car position
switch(FEAT_TRAINS, SELF, switch_BR68_5a, position_in_articulated_veh % 6) {	
	0: sw_BR68_5a_locomotive_pushpull;
	5: sw_BR68_5a_DVT_pushpull;
	sw_BR68_5a_middle;
}

switch(FEAT_TRAINS, SELF, sw_BR68_5a_cargo_subtype_text, cargo_subtype)
{
	0:	return string(str_TPE);
	1: 	return string(str_Chiltern2ToneBlue);
}

// Define articulations
switch(FEAT_TRAINS, SELF, switch_BR68_5a_articulated, extra_callback_info1) {
	// extra_callback_info1 returns the car number, so 1 is the second car, .. so on
	// The callback runs this switch until it returns NO_MORE_ARTICULATED_PARTS, which ends the growth
	1..5: return item_BR68_5a;
	return CB_RESULT_NO_MORE_ARTICULATED_PARTS; // Stop adding more cars
}

// Start/stop switch, used to limit maximum length
switch(FEAT_TRAINS, SELF, switch_BR68_5a_start_stop, num_vehs_in_consist) {	
	return CB_RESULT_NO_TEXT;   // Allow any length
}

// Only allow the same train type to be added to itself
switch(FEAT_TRAINS, SELF, switch_BR68_5a_can_attach_wagon, vehicle_type_id) {
	item_BR68_5a: return CB_RESULT_ATTACH_ALLOW;
	return string(STR_CANNOT_ATTACH);
}

// Only the driving engine has particles
switch(FEAT_TRAINS, SELF, switch_BR68_5a_particles, position_in_articulated_veh % 6) {
	0: visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 2, DISABLE_WAGON_POWER);
	visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}

// Class 195/0 (2car)
item(FEAT_TRAINS, item_BR68_5a, 442) {
	property {
		variant_group: header_68;
		name: string(STR_BR68_5a);
		climates_available: ALL_CLIMATES; // available in all climates
		introduction_date: date(2019, 08, 04);
		model_life: VEHICLE_NEVER_EXPIRES; // keep available forever		
		vehicle_life: 30; // years after vehicle is deemed "old" and should be replaced
		reliability_decay: 0; // dont reduce reliabilty, (will grow from 75% upwards over the years)
		refittable_cargo_classes: bitmask(CC_PASSENGERS);
		non_refittable_cargo_classes: bitmask();
		cost_factor: 65;
		loading_speed: 5;
		running_cost_factor: 100;
		cargo_allow_refit: [];
		cargo_disallow_refit: [];
		
		speed: 100 mph;
		power: 3800 hp;
		cargo_capacity: (291/6);
		weight: 299 ton; 
		
		sprite_id: SPRITE_ID_NEW_TRAIN; // required
		refit_cost: 0;
		track_type: RAIL;
		ai_special_flag: AI_FLAG_PASSENGER; // tell ai that this is a passenger train
		running_cost_base: RUNNING_COST_DIESEL;
		dual_headed: 0; // set to 0 otherwise limited to 2 cars only
		engine_class: ENGINE_CLASS_DIESEL; // even if ts 3rd rail, ELECTRIC would give overhead wire effects
		extra_power_per_wagon: 0 kW;
		tractive_effort_coefficient: 0.37;
		air_drag_coefficient: 0.06;
		length: 8; // full tile length vehicle
		visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 2, DISABLE_WAGON_POWER); // visual fx
		extra_weight_per_wagon: 0 ton;
		bitmask_vehicle_info: 0;
	}
	
	graphics {
		default: switch_BR68_5a;
		cargo_subtype_text: sw_BR68_5a_cargo_subtype_text;
		purchase: spriteset_BR68_5a_purchase;
		
		articulated_part: switch_BR68_5a_articulated;
		start_stop: switch_BR68_5a_start_stop;	
		can_attach_wagon: switch_BR68_5a_can_attach_wagon;
		visual_effect_and_powered: switch_BR68_5a_particles;
				
		cargo_capacity: return (291/6 * param_pax);
		additional_text: string(STR_DSC_BR68_MK5A);	
		cost_factor:	return(GetAdjustedCost(65));
		running_cost_factor: return(GetAdjustedCost(100));
	}
}