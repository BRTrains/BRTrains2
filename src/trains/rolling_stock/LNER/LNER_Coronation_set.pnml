switch(FEAT_TRAINS, SELF, sw_CoronationCoach_articulated, extra_callback_info1) {
	// extra_callback_info1 returns the car number, so 1 is the second car, .. so on
	// The callback runs this switch until it returns NO_MORE_ARTICULATED_PARTS, which ends the growth
    0..8: return item_LNER_Cornation_Set;
	return CB_RESULT_NO_MORE_ARTICULATED_PARTS; // Stop adding more cars
}

switch(FEAT_TRAINS, SELF, sw_Coronation_Set_CorBlue, position_in_articulated_veh ) {
    0: spriteset_LNER_CorSet_BT_CorBlue;
    1: spriteset_LNER_CorSet_KTRev_CorBlue;
    2: spriteset_LNER_CorSet_OF_CorBlue;
    3: spriteset_LNER_CorSet_OFRev_CorBlue;
    4: spriteset_LNER_CorSet_OT_CorBlue;
    5: spriteset_LNER_CorSet_KTRev_CorBlue;
    6: spriteset_LNER_CorSet_OT_CorBlue;
    7: spriteset_LNER_CorSet_BTRev_CorBlue;
    8: spriteset_LNER_CorSet_OBV_CorBlue;
}

switch(FEAT_TRAINS, SELF, sw_Coronation_Set_BnC, position_in_articulated_veh ) {
    0: spriteset_LNER_CorSet_BT_BnC;
    1: spriteset_LNER_CorSet_KTRev_BnC;
    2: spriteset_LNER_CorSet_OF_BnC;
    3: spriteset_LNER_CorSet_OFRev_BnC;
    4: spriteset_LNER_CorSet_OT_BnC;
    5: spriteset_LNER_CorSet_KTRev_BnC;
    6: spriteset_LNER_CorSet_OT_BnC;
    7: spriteset_LNER_CorSet_BTRev_BnC;
    8: spriteset_LNER_CorSet_OBV_BnC;
}

switch(FEAT_TRAINS, SELF, sw_Coronation_Set_liveries, cargo_subtype) {
    0: sw_Coronation_Set_CorBlue;
    1: sw_Coronation_Set_BnC;
}

switch(FEAT_TRAINS, SELF, sw_Coronation_Set_cargo_subtype_text, cargo_subtype) {
    0: return string(STR_LIVERY, string(str_abbr_LNER), string(str_col_GarterBlue));
	1: return string(STR_LIVERY, string(str_abbr_BR), string(str_col_BloodCustard));
       return CB_RESULT_NO_TEXT;
}

switch(FEAT_TRAINS, SELF, sw_CoronationCoach_Capacity, position_in_articulated_veh ) {
    0: 36;  // BT
    1: 6;   // KT
    2: 24;  // OF
    3: 24;  // OF
    4: 30;  // OT
    5: 6;   // KT
    6: 30;  // OT
    7: 36;  // BT
    8: 16;  // OBV
    // total 208
}

item (FEAT_TRAINS, item_LNER_Cornation_Set, 711) {
    property {
        name:                           string(STR_NAME_LNER_CorSet);
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1937, 7, 5);
        model_life:                     30;
        cargo_allow_refit:              [PASS];
		loading_speed:					10;
		cost_factor:					1;
		running_cost_factor:			1;
		power:							0;
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		speed:							110 mph;
		refit_cost:						0;
		running_cost_base:				RUNNING_COST_STEAM;
		default_cargo_type:				PASS;
		cargo_capacity:					1;
		weight:							342 ton;
		length:							8;
		bitmask_vehicle_info:			0;
    }
    graphics {
        additional_text:                return(string(str_purchase_coach, string(STR_LNER_CorSet_liveries)));
        articulated_part:               sw_CoronationCoach_articulated;

        can_attach_wagon:               CB_RESULT_ATTACH_DISALLOW;
        cargo_capacity:                 sw_CoronationCoach_Capacity;
        cargo_subtype_text:             sw_Coronation_Set_cargo_subtype_text;
        colour_mapping:                 return PALETTE_CC_FIRST;
        cost_factor:                    return(GetAdjustedCost(25));
        
        default:                        sw_Coronation_Set_liveries;
        purchase:                       spriteset_LNER_CorSet_Purchase;
        purchase_cargo_capacity:        return (208 / 9 * param_pax);
        running_cost_factor:            return(GetAdjustedCost(25));
    }
}