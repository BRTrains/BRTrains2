spriteset(spriteset_Standard9F_Purchase, "gfx/Steam/Standard9F.png") {
	template_purchase_anim(0, 0)
}

spriteset(spriteset_Standard9F_BRBlack, "gfx/Steam/Standard9F.png") {
	template_train32px_anim(0, 13)
}
spriteset(spriteset_Standard9FTender_BRBlack, "gfx/Steam/Standard9F.png") {
	template_train16px(0, 38)
}

spriteset(spriteset_Standard9F_BRGreen, "gfx/Steam/Standard9F.png") {
	template_train32px_anim(0, 56)
}
spriteset(spriteset_Standard9FTender_BRGreen, "gfx/Steam/Standard9F.png") {
	template_train16px(0, 81)
}

switch(FEAT_TRAINS,SELF, sw_Standard9FLoco,cargo_subtype){
    0:	spriteset_Standard9F_BRBlack;
	1: 	spriteset_Standard9F_BRGreen;
}

switch(FEAT_TRAINS,SELF, sw_Standard9FTender,cargo_subtype){
    0:	spriteset_Standard9FTender_BRBlack;
	1: 	spriteset_Standard9FTender_BRGreen;
}

switch(FEAT_TRAINS,SELF,sw_Standard9F_cargo_subtype_text,cargo_subtype){
    0: return string(str_BRBlack);
    1: return string(str_BRGreen);
       return CB_RESULT_NO_TEXT;
}

switch(FEAT_TRAINS, SELF, sw_Standard9F, position_in_articulated_veh){
	0: sw_Standard9FLoco;
	1: sw_Standard9FTender;
}

switch(FEAT_TRAINS, SELF, sw_Standard9F_length, position_in_articulated_veh) {	
	0: return 8;
	1: return 4;
}

switch (FEAT_TRAINS, SELF, sw_articulated_Standard9F, extra_callback_info1) {
	1: return item_Standard9F;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS; //stop adding vehicle parts
}

item (FEAT_TRAINS, item_Standard9F, 587) {
	property {
		name: 							string(STR_NAME_Standard9F);
		climates_available: 			ALL_CLIMATES;
		introduction_date:				date(1954,2,4);
		model_life:						20;
		retire_early:					1;
		vehicle_life:					20;
		reliability_decay:				7;
		refittable_cargo_classes:		0;
		cargo_allow_refit:				[GOOD, FOOD];
		loading_speed:					3;
		cost_factor:					26;
		running_cost_factor:			30;
		sprite_id:						SPRITE_ID_NEW_TRAIN;
		speed:							90 mph;
		misc_flags:						TRAIN_FLAG_NO_BREAKDOWN_SMOKE;
		refit_cost:						0;
		track_type:						RAIL;
		ai_special_flag:				AI_FLAG_CARGO;
		power:							2400 hp;
		running_cost_base:				RUNNING_COST_STEAM;
		dual_headed:					0;
		default_cargo_type:				GOOD;
		cargo_capacity:					1;
		weight:							139 ton;
		engine_class:					ENGINE_CLASS_STEAM;
		tractive_effort_coefficient:	0.3;
		air_drag_coefficient:			0.1;
		length:							8;
		effect_spawn_model_and_powered:	EFFECT_SPAWN_MODEL_STEAM;
		extra_weight_per_wagon:			0;
		bitmask_vehicle_info:			0;
	}
	graphics {
	additional_text:					return(string(str_purchase_loco_with_liveries,string(str_purchase_type_steam),string(str_route_9),string(str_Standard9F_usage),string(str_Standard9F_eos),string(str_Standard9F_liveries)));
	can_attach_wagon:					CB_RESULT_ATTACH_ALLOW;
	cargo_capacity:						return 0;
	cargo_subtype_text:					sw_Standard9F_cargo_subtype_text;
	articulated_part:        			sw_articulated_Standard9F;
	default: 							sw_Standard9F;
	purchase:							spriteset_Standard9F_Purchase;
	colour_mapping: 					return PALETTE_CC_FIRST;
	create_effect:						steam_create_visual_effect;
	sound_effect:						sw_steam_sound;
	cost_factor:						return(GetAdjustedCost(26));
	running_cost_factor:				return(GetAdjustedCost(30));
	length:								sw_Standard9F_length;
	}
}